version = '1.0'

apply plugin: 'jetty'
apply plugin: 'eclipse-wtp'

sourceCompatibility = 1.7

sourceSets {
	selenium
}

repositories {
	mavenCentral()
}

dependencies {

	/* JDBC Driver */
	compile 'postgresql:postgresql:9.1-901-1.jdbc4'

	/* Hibernate */
	compile 'org.hibernate:hibernate-core:4.1.9.Final'
	compile 'org.hibernate:hibernate-validator:4.2.0.Final'

	/* Spring */
	compile('org.springframework:spring-context:3.2.0.RELEASE') {
		exclude group: 'commons-logging', module: 'commons-logging'
	}
	compile 'org.springframework:spring-orm:3.2.0.RELEASE'
	compile 'org.springframework:spring-webmvc:3.2.0.RELEASE'

	/* Logging */
	compile 'log4j:log4j:1.2.17'
	compile 'org.slf4j:slf4j-log4j12:1.6.1'

	/* Servlet */
	providedCompile 'javax.servlet:servlet-api:2.5'
	providedCompile 'javax.servlet.jsp:jsp-api:2.1'
	compile 'javax.servlet:jstl:1.2'

	/* Test */
	testCompile 'junit:junit:4.11'
	testCompile 'org.springframework:spring-test:3.2.0.RELEASE'
	
	/* Selenium */
	seleniumCompile 'postgresql:postgresql:9.1-901-1.jdbc4'
	seleniumCompile 'junit:junit:4.11'
	seleniumCompile 'org.seleniumhq.selenium:selenium-java:2.28.0'
	
}

eclipse {
	classpath {
		plusConfigurations += configurations.seleniumCompile
	}
}

task jettyDaemon(type: org.gradle.api.plugins.jetty.JettyRun) {
	daemon = true
}

task selenium(type: Test, dependsOn: jettyDaemon) {
	testClassesDir = sourceSets.selenium.output.classesDir
	classpath = sourceSets.selenium.runtimeClasspath
}
